import java.nio.file.Files
import java.nio.file.Path
import java.nio.file.Paths
import java.nio.file.StandardOpenOption

/*
 * GPLv3 License
 *
 *  Copyright (c) KAGA by waicool20
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

buildscript {
    ext.kotlin_version = '1.1.3-2'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'java'
apply plugin: 'kotlin'

group 'com.waicool20'
version 'v1.7.2'
def kcAutoCompatibility = '2017-06-07'

sourceCompatibility = 1.8

task versioning << {
    Path file = Paths.get("$projectDir/src/main/resources/version.txt")
    String content = """
{
    "version": "$project.version",
    "kcAutoCompatibility": "$kcAutoCompatibility"
}
""".stripMargin().trim()
    Files.write(file, content.getBytes(), StandardOpenOption.CREATE, StandardOpenOption.TRUNCATE_EXISTING)
}

defaultTasks 'build'
build.dependsOn(tasks.versioning)
build.finalizedBy(tasks.shadowJar)
jar.enabled = false

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile group: 'org.ini4j', name: 'ini4j', version: '0.5.4'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile group: 'no.tornado', name: 'tornadofx', version: '1.7.9'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.5'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.5'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.5'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.8.5'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

shadowJar {
    classifier = null
    version = null
}

jar {
    manifest {
        attributes([
                'Main-Class': 'com.waicool20.kaga.KagaApp'
        ])
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
